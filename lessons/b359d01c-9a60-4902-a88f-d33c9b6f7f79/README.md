> –í–Ω–∏–º–∞–Ω–∏–µ: –µ—Å–ª–∏ –≤—ã –æ–±–Ω–∞—Ä—É–∂–∏–ª–∏ –≤ —É—Ä–æ–∫–µ –æ—à–∏–±–∫—É –∏–ª–∏ –Ω–µ—Ç–æ—á–Ω–æ—Å—Ç—å, —Å–º–µ–ª–æ –ø–∏—à–∏—Ç–µ –æ–± —ç—Ç–æ–º –≤ –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏—è—Ö –∏–ª–∏ –∞–≤—Ç–æ—Ä—É. –í—Å–µ –º—ã —É—á–∏–º—Å—è –∏ –æ—à–∏–±–∞–µ–º—Å—è ü§ì

# –í–≤–µ–¥–µ–Ω–∏–µ

[–í–∏–¥–µ–æ –æ–±—Å—É–∂–¥–µ–Ω–∏–µ —É—Ä–æ–∫–∞.](https://youtu.be/vt5fpE0bzSY)

–ë—É–¥–µ–º –ø–∏—Å–∞—Ç—å —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç –¥–ª—è TON.

–°—Ç—É–∫—Ç—É—Ä–∞ —É—Ä–æ–∫–∞:

0. –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –Ω–µ–æ–±—Ö–æ–¥–∏–º—ã–µ –∏–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã
1. –ü–∏—à–µ–º –Ω–µ–±–æ–ª—å—à–æ–π —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç –Ω–∞ func, —Ç–µ—Å—Ç, –Ω–∞—á–∞–ª—å–Ω—ã–µ –¥–∞–Ω–Ω—ã–µ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ –∏ —Å–æ–æ–±—â–µ–Ω–∏–µ –¥–ª—è —É–≤–µ–ª–∏—á–µ–Ω–∏—è counter'–∞.
2. –°–ª–µ–ø–æ –∏—Å–ø–æ–ª—å–∑—É–µ–º toncli (simple) –¥–ª—è –≤–∑–∞–∏–º–æ–¥–µ–π—Å—Ç–≤–∏—è —Å–æ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ä–∞–∫—Ç–æ–º (–±–∏–ª–¥, –¥–µ–ø–ª–æ–π, –æ—Ç–ø—Ä–∞–≤–∫–∞ —Å–æ–æ–±—â–µ–Ω–∏–π)
3. –û—Å–æ–∑–Ω–∞–Ω–Ω–æ –∏—Å–ø–æ–ª—å–∑—É–µ–º fift/func/lite-client (advanced) –¥–ª—è –≤–∑–∞–∏–º–æ–¥–µ–π—Å—Ç–≤–∏—è —Å–æ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–æ–º (–±–∏–ª–¥, –¥–µ–ø–ª–æ–π, –æ—Ç–ø—Ä–∞–≤–∫–∞ —Å–æ–æ–±—â–µ–Ω–∏–π)


# –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –Ω–µ–æ–±—Ö–æ–¥–∏–º—ã–µ –∏–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã
–ü–æ–¥–≥–æ—Ç–∞–≤–ª–∏–≤–∞–µ–º —Å–≤–æ–µ –æ–∫—Ä—É–∂–µ–Ω–∏–µ.

```code
$ mkdir ~/tondevmoscow/lesson1/ & cd ~/tondevmoscow/lesson1/
$ python3 -m venv venv & source venv/bin/activate
$ pip install toncli wton
$ wton init
$ wton config provider.dapp.api_key 8d22e52d-da87-422e-9ace-095539a95706
$ wton keystore new tondev
Password[]:
$ wton config wton.keystore_name tondev
$ wton config --network testnet
```

toncli - —Ä–∞–±–æ—Ç–∞ —Å–æ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞–º–∏

wton   - —É–ø—Ä–∞–≤–ª–µ–Ω–∏–µ –∫–æ—à–µ–ª—å–∫–∞–º–∏, –ø–æ—Ç—Ä–µ–±—É–µ—Ç—Å—è –≤ advanced —É—Ä–æ–∫–µ (8d22e52d-da87-422e-9ace-095539a95706 - —Å–ø–µ—Ü–∏–∞–ª—å–Ω–æ –≤—ã–¥–µ–ª–µ–Ω–Ω—ã–π –∫–ª—é—á –¥–ª—è —É—Ä–æ–∫–∞)

–î–ª—è –∏–∑—É—á–µ–Ω–∏—è –ø–∞–∫–µ—Ç–æ–≤ [toncli](https://pypi.org/project/toncli/) –∏ [wton](https://pypi.org/project/wton/) –º–æ–∂–Ω–æ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å –∫–æ–º–∞–Ω–¥—É:
```code
$ wton -h
$ toncli -h
```

–í—ã –º–æ–∂–µ—Ç–µ –∑–∞–ø—É—Å–∫–∞—Ç—å –∫–æ–º–∞–Ω–¥—É -h (--help) –¥–ª—è –ª—é–±–æ–π –ø–æ–¥–∫–æ–º–∞–Ω–¥—ã wton
```code
$ wton wallet -h
$ wton wallet transfer -h
```

–î–ª—è —Ç–æ–≥–æ, —á—Ç–æ–±—ã –∏—Å–∫–∞—Ç—å –Ω–µ–ø–æ–Ω—è—Ç–Ω—ã–µ –≤–µ—â–∏, –º–æ–∂–Ω–æ —Å—Ä–∞–∑—É –æ—Ç–∫—Ä—ã—Ç—å –∏ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å (ctrl+f):
- FIFT - https://ton-blockchain.github.io/docs/fiftbase.pdf
- Blockchain - https://ton-blockchain.github.io/docs/tblkch.pdf
- TVM  - https://ton-blockchain.github.io/docs/tvm.pdf
- TLB  - https://github.com/ton-blockchain/ton/blob/master/crypto/block/block.tlb
- testnet.tonscan - https://testnet.tonscan.org/address/


# –ü–∏—à–µ–º —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç
–ù–∞–º –Ω—É–∂–Ω–∞ —Å–ª–µ–¥—É—é—â–∞—è —Å—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–ª—è –Ω–∞–ø–∏—Å–∞–Ω–∏—è —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞:
```code
project
‚îú‚îÄ‚îÄ project.yaml
‚îú‚îÄ‚îÄ func
‚îÇ   ‚îú‚îÄ‚îÄ *.fc
‚îú‚îÄ‚îÄ fift
‚îÇ   ‚îú‚îÄ‚îÄ *.fif
‚îú‚îÄ‚îÄ build
‚îÇ   ‚îú‚îÄ‚îÄ *
```


–ú–æ–∂–Ω–æ –≤–æ—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å—Å—è –∫–æ–º–∞–Ω–¥–æ–π –∏ —É–¥–∞–ª–∏—Ç—å –Ω–µ–Ω—É–∂–Ω—ã–µ —Ñ–∞–π–ª—ã (–≤ —Ü–µ–ª—è—Ö –æ–±—É—á–µ–Ω–∏—è —Ä–µ–∫–æ–º–µ–Ω–¥—É–µ–º —Å–¥–µ–ª–∞—Ç—å –≤—Å–µ —Å–≤–æ–∏–º–∏ —Ä—É–∫–∞–º–∏):
```code
$ toncli start wallet
```


- project.yaml - –ø—É—Ç–∏ –¥–æ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞/—Ç–µ—Å—Ç–æ–≤/–¥–∞–Ω–Ω—ã—Ö –¥–ª—è –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏–∏
- func  - –≤ —ç—Ç–æ–π –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–∏ —Ö—Ä–∞–Ω–∏–º –Ω–∞—à func –∫–æ–¥ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–æ–≤
- fift  - –≤ —ç—Ç–æ–π –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–∏ —Ö—Ä–∞–Ω–∏–º –Ω–∞—à fift –∫–æ–¥ (–ù–∞–ø—Ä–∏–º–µ—Ä, init data cell)
- build - –≤ —ç—Ç–æ–π –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–∏ –±—É–¥—É—Ç –ª–µ–∂–∞—Ç—å —Å–∫–æ–º–ø–∏–ª–∏—Ä–æ–≤–∞–Ω–Ω—ã–µ –∏ –¥—Ä—É–≥–∏–µ –Ω–µ–æ–±—Ö–æ–¥–∏–º—ã–µ(.addr, .pk) —Ñ–∞–π–ª—ã 



1Ô∏è‚É£ –í –∫–∞–∂–¥–æ–π –∏–∑ –≤–µ—Ä—Å–∏–π —Å–∞–º —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç –±—É–¥–µ—Ç –Ω–∞—Ö–æ–¥–∏—Ç—å—Å—è –≤ –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–∏ ./func. –ù–∞–ø–∏—à–µ–º –∫–∞—É–Ω—Ç–µ—Ä –≤–Ω—É—Ç—Ä–µ–Ω–Ω–∏—Ö –∏ –≤–Ω–µ—à–Ω–∏—Ö —Å–æ–æ–±—â–µ–Ω–∏–π + –æ—Ç—Å–ª–µ–∂–∏–≤–∞–µ–º —á–∏—Å–ª–æ —Å–æ–æ–±—â–µ–Ω–∏–π –æ—Ç –≤–ª–∞–¥–µ–ª—å—Ü–∞ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞:
```code
int equal_slices (slice a, slice b) asm "SDEQ";

(int, int, int, int, slice, int) load_data() {
    slice ds = get_data().begin_parse();
    return (ds~load_uint(32), ds~load_uint(32), ds~load_uint(32), ds~load_uint(32), ds~load_msg_addr(), ds~load_uint(256));
}

() store_data(int external_counter, int internal_counter, int owner_external_counter, int owner_internal_counter, slice owner_address, int public_key) impure {
    set_data(
        begin_cell()
            .store_uint(external_counter, 32)
            .store_uint(internal_counter, 32)
            .store_uint(owner_external_counter, 32)
            .store_uint(owner_internal_counter, 32)
            .store_slice(owner_address)
            .store_uint(public_key, 256)
            .end_cell()
    );
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();
	throw_if(35, in_msg_body.slice_bits() < 32);
    int n = in_msg_body~load_uint(32);

    var (external_counter, internal_counter, owner_external_counter, owner_internal_counter, owner_address, public_key) = load_data();
    internal_counter += n;
    if (equal_slices(sender_address, owner_address)) {
        owner_internal_counter += n;
    }

    store_data(external_counter, internal_counter, owner_external_counter, owner_internal_counter, owner_address, public_key);
}

() recv_external(slice in_msg) impure {
    int is_owner = 0;  ;; 0 - false; -1 - true
    int n = 0;
    var (external_counter, internal_counter, owner_external_counter, owner_internal_counter, owner_address, public_key) = load_data();

    if (in_msg.slice_bits() == 32) {
        n = in_msg~load_uint(32);
    } else {
        throw_if(35, in_msg.slice_bits() < 512 + 32);
        var signature = in_msg~load_bits(512);
        n = in_msg~load_uint(32);
        int is_owner = check_signature(slice_hash(in_msg), signature, public_key);
    }

    accept_message();

    external_counter += n;
    if (is_owner) {
        owner_external_counter += n;
    }

    store_data(external_counter, internal_counter, owner_external_counter, owner_internal_counter, owner_address, public_key);
}

;; Get methods
(int, int, int, int, slice, int) get_all() method_id {
    var cs = get_data().begin_parse();
    return load_data();
}

int get_external_counter() method_id {
    var cs = get_data().begin_parse();
    return cs~load_uint(32);
}

int get_internal_counter() method_id {
    var cs = get_data().begin_parse();
    cs~load_uint(32);
    return cs~load_uint(32);
}

int get_owner_external_counter() method_id {
    var cs = get_data().begin_parse();
    cs~load_uint(32);
    cs~load_uint(32);
    return cs~load_uint(32);
}

int get_owner_internal_counter() method_id {
    var cs = get_data().begin_parse();
    cs~load_uint(32);
    cs~load_uint(32);
    cs~load_uint(32);
    return cs~load_uint(32);
}
```


2Ô∏è‚É£ –ù–∞–ø–∏—à–µ–º –Ω–µ–±–æ–ª—å—à–æ–π —Ç–µ—Å—Ç –∫ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç—É (–µ–≥–æ –ø–æ–ª–æ–∂–∏–º –≤ tests/test.fc):
```code
[int, tuple, cell, tuple, int] test_internal_message() method_id(0) {
	int function_selector = 0;

	cell message = begin_cell()
				.store_uint(1618, 32)
				.end_cell();

	tuple stack = unsafe_tuple([message.begin_parse()]);
    
    cell owner_addr = begin_cell()
			.store_uint(1, 2)
			.store_uint(5, 9) 
			.store_uint(8, 5)
            .end_cell();

    cell data = begin_cell()
            .store_uint(0, 32)
            .store_uint(0, 32)
            .store_uint(0, 32)
            .store_uint(0, 32)
            .store_slice(owner_addr.begin_parse())
            .store_uint(1, 256)
            .end_cell();


	return [function_selector, stack, data, get_c7(), null()];
}


_ test_example(int exit_code, cell data, tuple stack, cell actions, int gas) method_id(1) {
	throw_if(100, exit_code != 0);

	var ds = data.begin_parse();
    ds~load_uint(32); ;; skip external_counter

	throw_if(101, ds~load_uint(32) != 10);
	throw_if(102, gas > 1000000); 
}
```



3Ô∏è‚É£ –¢–µ–ø–µ—Ä—å –Ω–∞–º –Ω—É–∂–Ω–æ –æ–ø–∏—Å–∞—Ç—å —Å—Ç–∞—Ä—Ç–æ–≤—ã–µ –¥–∞–Ω–Ω—ã–µ —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞. –û–Ω–∏ –±—É–¥—É—Ç –ª–µ–∂–∞—Ç—å –≤ –ø–∞–ø–∫–µ ./fift –∏ –≤—ã–≥–ª—è–¥—è—Ç —Ç–∞–∫:
```code
"TonUtil.fif" include
"Asm.fif" include

// AUTOGENERATED WALLET PATH ON MAC
"/Users/USERNAME/Library/Application Support/toncli/wallet/build/contract.pk" load-keypair // load owner key pair
constant owner_private_key
constant owner_public_key

// AUTOGENERATED WALLET PATH ON MAC
"/Users/USERNAME/Library/Application Support/toncli/wallet/build/contract.addr" load-address // load owner address
2dup 2constant owner_address
."INFO: Owner wallet address = " 2dup 6 .Addr cr


<b
  0 32 u, // external_counter
  0 32 u, // internal_counter
  0 32 u, // owner_external_counter
  0 32 u, // owner_internal_counter
  owner_address addr,
  owner_public_key B,
b>
```


4Ô∏è‚É£ –¢–∞–∫–∂–µ –Ω–∞–º –ø–æ—Ç—Ä–µ–±—É–µ—Ç—Å—è –æ–ø–∏—Å–∞—Ç—å —Ç–µ–ª–æ —Å–æ–æ–±—â–µ–Ω–∏—è, –∫–æ—Ç–æ—Ä–æ–µ –º—ã —Ö–æ—Ç–∏–º –æ—Ç–ø—Ä–∞–≤–ª—è—Ç—å –≤ –Ω–∞—à —Å–º–∞—Ä—Ç –∫–æ–Ω—Ç—Ä–∞–∫—Ç. –û–Ω–æ —Ç–æ–∂–µ –±—É–¥–µ—Ç –ª–µ–∂–∞—Ç—å –≤ –ø–∞–ø–∫–µ ./fift. –ö–æ–¥ —Ç–µ–ª–∞ —Å–æ–æ–±—â–µ–Ω–∏—è (–ø—Ä–æ—Å—Ç–æ –∫–ª–∞–¥–µ–º –≤ cell –ª—é–±–æ–µ 32-—Ö —Ä–∞–∑—Ä—è–¥–Ω–æ–µ —á–∏—Å–ª–æ):
```code
"Asm.fif" include

<b
  1618 32 u,
b>
```


–¢–µ–ø–µ—Ä—å, –∫–æ–≥–¥–∞ –º—ã —Ä–∞—Å–º–æ—Ç—Ä–µ–ª–∏ –∏—Å–ø–æ–ª—å–∑—É–µ–º—ã–π –∫–æ–¥, –ø–µ—Ä–µ–π–¥–µ–º –Ω–∞ —Å–ª–µ–¥—É—é—â–∏–π —à–∞–≥: simple/second_step.md
